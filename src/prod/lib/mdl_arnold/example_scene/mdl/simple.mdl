/******************************************************************************
 * Copyright (c) 2020-2025, NVIDIA CORPORATION. All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *  * Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 *  * Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 *  * Neither the name of NVIDIA CORPORATION nor the names of its
 *    contributors may be used to endorse or promote products derived
 *    from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS ``AS IS'' AND ANY
 * EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
 * PURPOSE ARE DISCLAIMED.  IN NO EVENT SHALL THE COPYRIGHT OWNER OR
 * CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
 * EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
 * PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY
 * OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 ******************************************************************************/

mdl 1.3;

import ::df::*;
import ::state::*;
import ::math::*;

export material diffuse(
    color tint = color(0.8, 0.6, 0.1)
) = material(
    surface: material_surface(
        scattering: df::diffuse_reflection_bsdf(tint: tint)
    )
);

export material glossy(
    color tint = color(0.8, 0.6, 0.1),
    color tint_back = color(0.1, 0.35, 0.05),
    float roughness = 0.2,
    int stripes = 16
) = let
{
    bsdf layer = df::simple_glossy_bsdf(roughness_u: roughness);
    float3 tex_coord3 = state::texture_coordinate(0);
    float opacity = math::sin(tex_coord3.x * math::PI * float(stripes));
    float opacity_pow = 1.0f - math::pow(opacity, 4.0f);
} in material(
    surface: material_surface(
        scattering: df::fresnel_layer(
            base: df::diffuse_reflection_bsdf(tint : tint),
            layer: layer,
            ior: color(1.5)
        )
    ),
    backface: material_surface(
        scattering : df::fresnel_layer(
            base : df::diffuse_reflection_bsdf(tint : tint_back),
            layer : layer,
            ior : color(1.5)
        )
    ),
    geometry: material_geometry(
        cutout_opacity : opacity_pow
    ),
    thin_walled: true
);

export material rough_glass(
    uniform float ior = 1.4,
    float roughness = 0.01
) = material(
    surface: material_surface(
        scattering: df::microfacet_ggx_smith_bsdf(
            roughness_u:  roughness,
            mode: df::scatter_reflect_transmit
        )
    ),
    ior: color(ior)
);

export material glow(
    color emission_color = color(0.25, 0.5, 0.75),
    float intensity = 10.0
) = material(
    surface: material_surface(
        emission:  material_emission (
            emission: df::diffuse_edf(),
            intensity: emission_color * intensity
        )
    )
);
